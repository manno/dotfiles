# .zshrc: interactive shell, after zprofile(login) and zshenv(always)
# vim: ft=zsh ts=4 sw=4

## Checks
[ -d $HOME/.zsh ] || echo "no resource&config dir: .zsh"

## Configure PATH (ARM64 Mac)
typeset -U PATH path  # Keep unique elements in both
path=(
    /opt/homebrew/{bin,sbin}
    /usr/local/{bin,sbin}
    ~/bin
    $HOME/.local/bin
    $HOME/go/bin
    /sbin
    /usr/sbin
    $path
)

## Load Libs
fpath=(~/.zsh/func $fpath)

## Parse ~/.zsh files
autoload load_zshd
load_zshd

# Only keep existing directories
path=($^path(N))

## Load more modules
autoload zargs

## Show ssh agent keys
if [ -x /usr/bin/ssh-add ] && [ "$SSH_AUTH_SOCK" != "" ] && [ -r "$SSH_AUTH_SOCK" ]; then
    /usr/bin/ssh-add -l
fi

## Needs to go after other bindkey statements
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# remove # from EXTENDED_GLOB
disable -p '#'
